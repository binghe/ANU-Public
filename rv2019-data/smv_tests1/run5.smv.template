-- Usefulness of Background Information - existence of shorter traces
-- M1: monitor with BI
-- M2: monitor w/o BI

-- The monitoring property is PROPERTY

#include "patterns/MONITOR.smv"
#include "patterns_with_BI/MONITORs.smv"
#include "model.smv"

MODULE main

VAR
  p : boolean;
  q : boolean;
  r : boolean;
  s : boolean;
  t : boolean;
  z : boolean;
  _p : { true, false, unknown };
  _q : { true, false, unknown };
  _r : { true, false, unknown };
  _s : { true, false, unknown };
  _t : { true, false, unknown };
  _z : { true, false, unknown };
  _reset : boolean;
  M1 : MONITORs (_p, _q, _r, _s, _t, _z, _reset);
  M2 : MONITOR  (_p, _q, _r, _s, _t, _z, _reset);
  SUS : system (s);
  c : 0 .. 50;

FROZENVAR
  diff : 0 .. 50; -- 50 is the length of random traces
INIT
  diff = 10;

DEFINE
  -- BI is valuable
  BIv := (M1._concl & !M2._concl);

-- disable soft resets
INVAR !_reset;

-- link SUS variable with M1/M2 variables
INVAR
  ((_p = true) -> p) & ((_p = false) -> !p) &
  ((_q = true) -> q) & ((_q = false) -> !q) &
  ((_r = true) -> r) & ((_r = false) -> !r) &
  ((_s = true) -> s) & ((_s = false) -> !s) &
  ((_t = true) -> t) & ((_t = false) -> !t) &
  ((_z = true) -> z) & ((_z = false) -> !z);

ASSIGN
  init(c) := 0;
  next(c) := (BIv & c < diff) ? (c + 1) : c;

-- This should only work for some but not all monitors:
INVARSPEC c < diff

LTLSPEC  !F G BIv
